plugins {
    id 'com.android.application'
}


android {
   signingConfigs {
        release {
            storeFile = file("keystore/android_keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }
    
    compileSdkVersion 34

    defaultConfig {
        def allowGoogleLogin = System.getenv('ALLOW_GOOGLE_LOGIN') == 'true'
        def baseDomains = System.getenv('ALLOWED_DOMAINS') ?: 'example.com'
        def allowedDomains = allowGoogleLogin ? "${baseDomains},accounts.google.com" : baseDomains
        buildConfigField "String", "ALLOWED_DOMAINS", "\"${allowedDomains}\""
        buildConfigField "boolean", "ALLOW_GOOGLE_LOGIN", "${allowGoogleLogin}"
        buildConfigField "String", "STARTUP_URL", "\"${System.getenv('STARTUP_URL') ?: '' }\""
        buildConfigField "String", "VIEW_MODE", "\"${System.getenv('VIEW_MODE') ?: 'AUTO'}\""
        buildConfigField "boolean", "BLOCK_MEDIA", "${System.getenv('BLOCK_MEDIA') ?: 'false'}"
        buildConfigField "boolean", "BLOCK_ADS", "${System.getenv('BLOCK_ADS') ?: 'true'}"
        buildConfigField "boolean", "NO_SSL", "${System.getenv('NO_SSL') ?: 'false'}"
        resValue "string", "app_name", System.getenv('APP_NAME') ?: "My Application"
        applicationId System.getenv('APPLICATION_ID') ?: "com.webview.myapplication"
        
        minSdkVersion 16
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.webview.myapplication'
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.13.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}